/*
 * Copyright 2000-2021 JetBrains s.r.o.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

sourceCompatibility = 1.8
project.archivesBaseName = 'annotations'

task mainJar(type: Jar) {
    into ("META-INF/versions/9", {
        from (project(':module-info').sourceSets.main.output, {
            // Skip extra files from common sources compiled with Java 9 target
            include("module-info.class")
        })
    })
    manifest.attributes("Multi-Release": true)
    duplicatesStrategy = DuplicatesStrategy.FAIL
}

task sourceJar(type: Jar) {
    into ("META-INF/versions/9", {
        from project(':module-info').sourceSets.main.java
    })
    duplicatesStrategy = DuplicatesStrategy.FAIL
    baseName = archivesBaseName + '-sources'
}

task javadocJar(type: Jar) {
    duplicatesStrategy = DuplicatesStrategy.FAIL
    from javadoc
}

artifacts {
    archives mainJar, sourceJar
}

mainJar {
    from sourceSets.main.output
}

sourceJar {
    from sourceSets.main.java
}

javadoc {
    source = [sourceSets.main.java]
}

model {
    publishing {
        publications {
            pluginMaven(MavenPublication) {
                artifactId archivesBaseName
                artifact mainJar
                artifact sourceJar {
                    classifier 'sources'
                }
                artifact javadocJar {
                    classifier 'javadoc'
                }
            }
        }
    }
}